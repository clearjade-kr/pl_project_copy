<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2//EN">
<html><head>





	<meta http-equiv="Content-Type" content="text/html;CHARSET=iso-8859-1">
	<meta name="GENERATOR" content="Visual Page 2.0 for Windows"><title>tinyada.bnf</title></head><body>

<h1 align="center">EBNF Grammar for TinyAda</h1>
<pre>In the following grammar<br><br>    =    means "is defined as"<br>    " "  enclose literal items<br>    [ ]  enclose items which may be omitted<br>    { }  enclose items which may appear zero or more times<br>    |    indicates a choice<br>    ( )  are used for grouping required choices<br>    &lt; &gt;  enclose semantic qualifications<br><br>compilation = subprogramBody<br><br>subprogramBody =<br>         subprogramSpecification "is"<br>         declarativePart<br>         "begin" sequenceOfStatements<br>         "end" [ &lt;procedure&gt;identifier ] ";"<br><br>declarativePart = { basicDeclaration }<br><br>basicDeclaration = objectDeclaration | numberDeclaration<br>         | typeDeclaration | subprogramBody<br><br>objectDeclaration =<br>         identifierList ":" typeDefinition ";"<br><br>numberDeclaration =<br>         identifierList ":" "constant" ":=" &lt;static&gt;expression ";"<br><br>identifierList = identifier { "," identifier }<br><br>typeDeclaration = "type" identifier "is" typeDefinition ";"<br><br>typeDefinition = enumerationTypeDefinition | arrayTypeDefinition<br>         | range | &lt;type&gt;name<br><br>range = "range " simpleExpression ".." simpleExpression<br><br>index = range | &lt;type&gt;name<br><br>enumerationTypeDefinition = "(" identifierList ")"<br><br>arrayTypeDefinition = "array" "(" index { "," index } ")" "of" &lt;type&gt;name<br><br>subprogramSpecification = "procedure" identifier [ formalPart ]<br><br>formalPart = "(" parameterSpecification { ";" parameterSpecification } ")"<br><br>parameterSpecification = identifierList ":" mode &lt;type&gt;name<br><br>mode = [ "in" ] | "in" "out" | "out" <br><br>sequenceOfStatements = statement { statement }<br><br>statement = simpleStatement | compoundStatement<br><br>simpleStatement = nullStatement | assignmentStatement<br>         | procedureCallStatement | exitStatement<br><br>compoundStatement = ifStatement | loopStatement<br><br>nullStatement = "null" ";"<br><br>assignmentStatement = &lt;variable&gt;name ":=" expression ";"<br><br>ifStatement =<br>         "if" condition "then" sequenceOfStatements<br>         { "elsif" condition "then" sequenceOfStatements }<br>         [ "else" sequenceOfStatements ]<br>         "end" "if" ";"<br><br>loopStatement =<br>         [ iterationScheme ] "loop" sequenceOfStatements "end" "loop" ";"<br><br>iterationScheme = "while" condition<br><br>exitStatement = "exit" [ "when" condition ] ";"<br><br>procedureCallStatement = &lt;procedure&gt;name [ actualParameterPart ] ";"<br><br>actualParameterPart = "(" expression { "," expression } ")"<br><br>condition = &lt;boolean&gt;expression<br><br>expression = relation { "and" relation } | { "or" relation }<br><br>relation = simpleExpression [ relationalOperator simpleExpression ]<br><br>simpleExpression =<br>         [ unaryAddingOperator ] term { binaryAddingOperator term }<br><br>term = factor { multiplyingOperator factor }<br><br>factor = primary [ "**" primary ] | "not" primary<br><br>primary = numericLiteral | name | "(" expression ")"<br><br>name = identifier [ indexedComponent ]<br><br>indexedComponent = "(" expression  { "," expression } ")"<br><br>relationalOperator = "=" | "/=" | "&lt;" | "&lt;=" | "&gt;" | "&gt;="<br><br>binaryAddingOperator = "+" | "-"<br><br>unaryAddingOperator = "+" | "-"<br><br>multiplyingOperator = "*" | "/" | "mod"<br></pre>

</body></html>